////////////////////////////////////////////////////////////////////////////////
// MAJOR MODE 5 (ABORT)
////////////////////////////////////////////////////////////////////////////////
FUNCTION MAJOR_MODE5_TRANSFER {
	// INITIALIZE DITCH RELATED PARAMETERS
	SET DITCH_CONTROLLER TO PIDLOOP(1.0, 0.0, 0.6, -2.0, 7.5).
	SET TARGET_HEADING TO 0.
	SET PREV_ALTITUDE TO 0.
	SET HNAV_DELTA TO 0.
	SET VNAV_DELTA TO 0.
	
	// INITIALIZE MAJOR MODE 3 TRANSFER
	MAJOR_MODE3_TRANSFER().
}

FUNCTION MAJOR_MODE5 {
	// COMPUTE VERTICAL VELOCITY
	SET VZ TO (SHIP:ALTITUDE - PREV_ALTITUDE)/DT.
	SET PREV_ALTITUDE TO SHIP:ALTITUDE.

	// EXECUTE ABORT CODE
	IF MINOR_MODE = 0 { // RETURN TO LAUNCH SITE
		// PREDICT MODE 3 PARAMETERS
		SET HNAV_DELTA TO (1000*(GEOPOSITION:LAT - RUNWAY:LAT)).
		SET HNAV TO MIN(MAX( HNAV_DELTA*0.25,-25),25).
		SET NAV_ALT TO SHIP:ALTITUDE - 71.
		IF RUNWAY:DISTANCE > FLARE_LENGTH { // ON GLIDE SLOPE
			SET SLOPE TO (RUNWAY:DISTANCE - FLARE_DISPLACEMENT)*TAN(MM3_GLIDE_ANG).
		} ELSE { // ON FLARE
			SET EXP_PARAM TO -MAX(0, FLARE_LENGTH - RUNWAY:DISTANCE)/FLARE_CONST.
			SET EXP_VALUE TO CONSTANT:E^EXP_PARAM.
			SET SLOPE TO (H0*EXP_VALUE - MM3_H1_PARAM).
		}
		SET VNAV_DELTA TO (NAV_ALT - (MM3_FINAL_ALT*0.75 + SLOPE)).
		
		// CONTROL RCS
		IF ((SHIP:Q < 0.05) AND (MINOR_MODE <= 2)) {
			RCS ON.
		} ELSE {
			RCS OFF.
		}
		
		// CONTROL BRAKES
		IF SHIP:Q > 0.15 { BRAKES ON. }
		IF SHIP:Q < 0.15 { BRAKES OFF. }
		
		// TRANSFER TO AUTO CONTROL
		IF ABS(VNAV_DELTA) < 300.0 {
			SAS OFF.
			TRANSFER_MODE(3,1).
		}
		
		// ENSURE NO STEERING
		UNLOCK STEERING.
	}
	IF MINOR_MODE = 1 { // ABORT TO ORBIT
		// FIXME	
	}
	IF MINOR_MODE = 2 { // DITCH
		// ENGAGE STEERING LOCK WHEN SHUTTLE IS CLOSE ENOUGH TO GROUND
		IF ALT:RADAR < 200 {
			// BLEND BETWEEN 7 M/S AND 1 M/S VERTICAL VELOCITY
			SET VNAV_MOD TO MAX(0, MIN(1, (ALT:RADAR-50)/100 )).
			// DITCH CONTROL
			SET VNAV_CMD TO DITCH_CONTROLLER:UPDATE(TIME:SECONDS, VZ+ (1+6*VNAV_MOD) ).
			
			// STEERING LOOP
			LOCK STEERING TO HEADING(TARGET_HEADING, VNAV_CMD).
			// DISABLE PILOT STABILIZATION
			SAS OFF.
			
			// CONTROL BRAKES
			IF SHIP:Q > 0.07 { BRAKES ON. }
			IF SHIP:Q < 0.07 { BRAKES OFF. }
		} ELSE {
			// LOCK HEADING
			SET POLE TO LATLNG(90,0).
			SET TARGET_HEADING TO MOD(360 - POLE:BEARING,360).
			
			// NO STEERING
			UNLOCK STEERING.
			
			// CONTROL BRAKES
			IF ALTITUDE > 500 {
				IF SHIP:Q > 0.15 { BRAKES ON. }
				IF SHIP:Q < 0.15 { BRAKES OFF. }
			} ELSE {
				IF SHIP:Q > 0.10 { BRAKES ON. }
				IF SHIP:Q < 0.10 { BRAKES OFF. }
			}
		}
		
		// CONTROL RCS
		IF ((SHIP:Q < 0.05) AND (MINOR_MODE <= 2)) {
			RCS ON.
		} ELSE {
			RCS OFF.
		}
	}

	// DISPLAY OUTPUT
	IF UI_UPDATE {
		PRINT_VAR(0, "Q",	"KPA",	4, ROUND(SHIP:Q*100,1),				14,0).
		PRINT_VAR(1, "ALT",	"M",	7, ROUND(ALTITUDE/100,0)*100,		0,1).
		PRINT_VAR(2, "APO",	"M",	7, ROUND(SHIP:APOAPSIS/100,0)*100,	0,2).
		PRINT_VAR(3, " +Z",	"M/S",	5, ROUND(VZ,1),						0,3).
		
		PRINT_VAR(4, "NAV +X",	"M",	5, 10*ROUND(RUNWAY:DISTANCE/10,0),	0,5).
		PRINT_VAR(5, "NAV +Y",	"M",	5, ROUND(HNAV_DELTA,1),				0,6).
		PRINT_VAR(6, "NAV +Z",	"M",	5, ROUND(VNAV_DELTA,1),				0,7).
	}
}
