// INITIALIZE TERMINAL
CLEARSCREEN.
SET TERMINAL:CHARWIDTH TO 12.
SET TERMINAL:CHARHEIGHT TO 12.
SET TERMINAL:WIDTH TO 20.
SET TERMINAL:HEIGHT TO 8.
SET MAJOR_MODE TO 0.
SET MINOR_MODE TO 0.

// OPEN A NEW DATA FILE
SET ID to 1.
UNTIL NOT EXISTS("DATA" + ID + ".CSV") {
    SET ID to ID + 1.
}
SET DATAFILE TO CREATE("DATA" + ID + ".CSV").

// SCAN ALL SENSORS
SET SENSOR_NAMES TO LIST().
SET SENSORS TO LIST().
SET SENSOR_POS TO LIST().

FOR PART IN SHIP:PARTS {
	SET PREFIX TO "".
	
	IF (PART:NAME = "sensorThermometer")	{ SET PREFIX TO "T_". }
	IF (PART:NAME = "sensorAccelerometer")	{ SET PREFIX TO "A_". }

	IF PREFIX <> "" {
		SET SENSOR_NAME TO PREFIX + PART:TAG.
		SET SENSOR_POSITION TO (-SHIP:FACING) * (PART:POSITION - SHIP:ROOTPART:POSITION).
	
		// ADD THE SENSOR SO LIST IS SORTED
		FOR IDX IN RANGE(SENSOR_NAMES:LENGTH) {
			IF SENSOR_NAME < SENSOR_NAMES[IDX] {
				SENSOR_NAMES:INSERT(IDX, SENSOR_NAME).
				SENSORS:INSERT(IDX, PART).
				SENSOR_POS:INSERT(IDX, SENSOR_POSITION).
				BREAK.
			}
		}
		IF NOT SENSOR_NAMES:CONTAINS(SENSOR_NAME) {
			SENSOR_NAMES:ADD(SENSOR_NAME).
			SENSORS:ADD(PART).
			SENSOR_POS:ADD(SENSOR_POSITION).
		}
	}
}

// PRINT FILE HEADER
DATAFILE:WRITE("TIME,").
FOR IDX IN RANGE(SENSOR_NAMES:LENGTH) {
	PRINT "SENSOR " + SENSOR_NAMES[IDX].
	DATAFILE:WRITE(SENSOR_NAMES[IDX] + ",").
}
DATAFILE:WRITELN("0").

// START LOGGING
WAIT 1.0.
CLEARSCREEN.
PRINT "MODULAR DATA SYSTEM" AT (0,0).
PRINT "    ACTIVE:" AT (0,2).
PRINT "      FILE:" AT (0,3).
PRINT "     INDEX:" AT (0,4).
PRINT "FREE SPACE:" AT (0,5).
PRINT "  CAPACITY:" AT (0,6).

// TOGGLE MADS
SET ACTIVE TO FALSE.
ON AG6 {
	TOGGLE ACTIVE.
	PRESERVE.
}

// KEEP WRITING TELEMETRY DATA
SET NUM TO 0.
UNTIL FALSE {
	// QUERY ALL SENSORS AND WRITE DATA
	IF ACTIVE {
		DATAFILE:WRITE(ROUND(TIME:SECONDS,2)+",").
		FOR IDX IN RANGE(SENSOR_NAMES:LENGTH) {
			IF SENSORS[IDX]:MODULES:CONTAINS("ModuleEnviroSensor") {
				SET VALUE TO SENSORS[IDX]:GETMODULE("ModuleEnviroSensor"):GETFIELD("display").
				IF VALUE = "OFF" {
					SENSORS[IDX]:GETMODULE("ModuleEnviroSensor"):DOACTION("toggle display", TRUE ).
					SET VALUE TO "0u".
				}
				IF SENSORS[IDX]:TAG:CONTAINS("RA") {
					SET SENSOR_POSITION TO (-SHIP:FACING) * (SENSORS[IDX]:POSITION - SHIP:ROOTPART:POSITION).
					SET STRAIN TO (SENSOR_POSITION - SENSOR_POS[IDX]):MAG*10000.
					IF STRAIN > 99999 {
						SET STRAIN TO 99999.
					}
					SET VALUE TO ROUND(STRAIN, 0) + "u".
				}
				
				DATAFILE:WRITE(VALUE:SUBSTRING(0,VALUE:LENGTH-1) + ",").
			} ELSE {
				DATAFILE:WRITE(",").
			}
		}
		DATAFILE:WRITELN("0").
		SET NUM TO NUM + 1.
	}
	
	// PRINT STATUS
	IF ACTIVE {
		PRINT "ON " AT (12,2).
	} ELSE {
		PRINT "OFF" AT (12,2).
	}
	//PRINT ((-SHIP:FACING) * (SENSORS[1]:POSITION - SHIP:ROOTPART:POSITION) - SENSOR_POS[1])*10000 AT (0,2).
	PRINT ID AT (12,3).
	PRINT NUM AT (12,4).
	PRINT PROCESSOR("MADS"):VOLUME():FREESPACE AT (12,5).
	PRINT PROCESSOR("MADS"):VOLUME():CAPACITY AT (12,6).
	
	// WRITE DELAY
	WAIT 0.10.
}